#define ChapterID VermilionGymCh
#define WildLvl 5 
#define TrainerL VermilionGymLevel





EventPointerTable(ChapterID,ThisChapter)



ALIGN 4
ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,GlobalTurnEvent,0,255)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0,GlobalTalkEvent, 0xFF, Ephraim)
END_MAIN



LocationBasedEvents:
House(0,ToVermilion,7,22)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
//AFEV 0 GlobalAfev 0
DefeatAll(EndingScene) 
//AFEV flagID eventOffset activationFlag
//AREA flagID eventOffset [corner1x, corner1y] [corner2x, corner2y]
//AFEV 0 RefreshIfNotInDanger 0 
//AREA 0 ToVermilion [7,24] [7,24] 
END_MAIN
#ifdef MultiTileMarker_Installer
{ ; MultiTileEntry(ChapterID) 
EscapeTileEntry(7,22) 
EscapeTileTerminator ; } 
#endif 

ToVermilion:
ChapterTransition(VermilionCh, 12,23)

Traps1:
GenericCompletion(7,3,LtSurgeSMS, DebugText) // requires flag 6 to be on (defeated all enemies). Displays SMS ID and uses "Talk" option to then display text 

ENDTRAP
ALIGN 4


BeginningScene:
CALL GlobalBeginningScene
CALL LoadAndPlacePlayers 
#ifdef INSTALL_FMU
DisableFreeMovement
#endif
CHECK_EVENTID ThunderbadgeObtained
BNE 10 rC r0 
ENUT TrainerBattleActiveFlag
MUSC 0x7FFF // Mute 
CAM1 [8,6] 


LOAD1 0x1 EnemyTrainersA
ENUN 
ENUT FIXED_GROWTHS_FLAG_ID
AutoLevelUnits(TrainerL, True, 0x90EF) 
ENUT FIXED_GROWTHS_FLAG_ID
AutoLevelUnits(VermilionGymBonus, False, 0x90EF) 
//AutoLevelUnits(3, False, 0x90EF) 
CALL GenericAutolevelEvent_Earlygame // Trainers given invisible levels based on difficulty 

SetUnitStatusAll(0xA0DF, 0x24, 0x5, 0, AnyDeployed, AnyArmy, AnyClass, AnyRange, AnyItem)
// 0x24 is Item 4 and 0x5 is type WORD, so deleting items 4-5 and their durability 

SilentGiveSkillAccessory(0xD0, LifeAndDeathID)
SilentGiveItemWithDurability(0xD0, SuperPotion,0x1)
SilentGiveItemWithDurability(0xA0, Berry,0x82)
SilentGiveItemWithDurability(0xA1, Berry,0x82)
SilentGiveItemWithDurability(0xA2, Berry,0x82)
SilentGiveItemWithDurability(0xA3, Berry,0x82)


MUSCFAST IntroBGM 
ShowMap
Text(LtSurgeChallengeText)



ASMC 0x80160D1 // Return to map BGM 

CAM1 [7,12] 
GOTO 99 

LABEL 10 // Rematch 
ENUT TrainerBattleActiveFlag
MUSC 0x7FFF // Mute 
CAM1 [8,6] 


LOAD1 0x1 EnemyTrainersA
ENUN 
ENUT FIXED_GROWTHS_FLAG_ID
AutoLevelUnits(RematchLevel, True, 0x90EF) // Trainers given +5 visible levels 
CALL GenericAutolevelEvent // Trainers given invisible levels based on difficulty 

SetUnitStatusAll(0xA0DF, 0x24, 0x5, 0, AnyDeployed, AnyArmy, AnyClass, AnyRange, AnyItem)
// 0x24 is Item 4 and 0x5 is type WORD, so deleting items 4-5 and their durability 

SilentGiveSkillAccessory(0xD0, LifeAndDeathID)
//SilentGiveItemWithDurability(0xD0, SuperPotion,0x1)
//SilentGiveItemWithDurability(0xA0, Berry,0x82)
//SilentGiveItemWithDurability(0xA1, Berry,0x82)
//SilentGiveItemWithDurability(0xA2, Berry,0x82)
//SilentGiveItemWithDurability(0xA3, Berry,0x82)


MUSCFAST IntroBGM 
ShowMap
Text(LtSurgeRematchText)



ASMC 0x80160D1 // Return to map BGM 

CAM1 [7,12]  

LABEL 99
NoFade
ENDA




EndingScene: 
CHECK_EVENTID ThunderbadgeObtained 
BNE 10 rC r0 
EVBIT_MODIFY 4 
MUSC 0x7FFF // Mute 
#ifdef UnsetAllDangerRadius
UnsetAllDangerRadius
#endif 
UNCR(0x7FFF, 0x43) // Show SMS & Refresh 
ASMC 0x80790a5 //MU_EndAll //deselect unit
MUSCSLOW GymBGM
Text(LtSurgeDefeatText)
GiveItemWithDurability(0, SpellScroll, Thunderbolt)
ENUT ThunderbadgeObtained 
ENUT VermilionGymClosedFlag
SVAL r3 6000 
GIVETOMAIN 0 
TEXTSHOW LtSurgeWaitText
TEXTEND 
REMA
GiveSkillAccessory(0, LifeAndDeathID)
TEXTSTART
TEXTSHOW LtSurgeRewardText
TEXTEND 
FADI 4 
REMA 
ForcedChapterTransition(VermilionCh, 12,23)
GOTO 99 

LABEL 10 // Rematch 
EVBIT_MODIFY 4 
MUSC 0x7FFF // Mute 
#ifdef UnsetAllDangerRadius
UnsetAllDangerRadius
#endif 
UNCR(0x7FFF, 0x43) // Show SMS & Refresh 
ASMC 0x80790a5 //MU_EndAll //deselect unit
MUSCSLOW GymBGM
Text(LtSurgeRematchDefeatText)
GiveItemWithDurability(0, SpellScroll, Agility)
ENUT VermilionGymRematchedFlag
SVAL r3 5000 
GIVETOMAIN 0 
FADI 4 
ForcedChapterTransition(VermilionCh, 12,23)


LABEL 99 
NoFade
ENDA

Dunno1:
END_MAIN
Dunno2:
END_MAIN
Dunno3:
END_MAIN
Tutorial:
END_MAIN
Traps2:
ENDTRAP
ALIGN 4

Units1: //only used for a pointless POIN at start 
UNIT Seth  0x7 0x1 0xA1     [0,0] 0b 0x0 0x0 0x0 [0x2,0x3,0x4,0x5] [0x0,0x0,0x0,0x0]
UNIT 


EnemyTrainersA:
UNIT 0xD0 RaichuID 0x0 Level(0, Enemy, 1) [7,8]  0b 0x0	0x1 REDA7R8 		[Thunderbolt,AttackPulse,GrassKnot,0x0] ZoningHeal50GroupOneAI
UNIT 0xA0 MagnemiteID 0xD0 Level(0, Enemy, 1) [6,10]  0b 0x0	0x1 REDA6R10 	[Thundershock,Tackle,ThunderWave,0x0] ZoningHeal50GroupOneAI
UNIT 0xA1 VoltorbID 0xD0 Level(0, Enemy, 1) [8,10]  0b 0x0	0x1 REDA8R10 	[Screech,ThunderWave,0x0,0x0] ZoningHeal50GroupOneAI
UNIT 0xA2 ElectabuzzID 0xD0 Level(0, Enemy, 1) [4,10]  0b 0x0 0x1 REDA4R10 [ThunderPunch,IcePunch,FirePunch,0x0] ZoningHeal50GroupOneAI
UNIT 0xA3 JolteonID 0xD0 Level(0, Enemy, 1) [10,10]  0b 0x0	0x1 REDA10R10 	[Thundershock,TailWhip,TakeDown,0x0] ZoningHeal50GroupOneAI
UNIT 0xA4 MarowakID 0xD0 Level(0, Enemy, 1) [7,12]  0b 0x0	0x1 REDA7R12 	[Bonemerang,Leer,0x0,0x0] ZoningHeal50GroupOneAI
UNIT

EnemyTrainersB:
UNIT 0xD0 RaichuID 0x0 Level(0, Enemy, 1) [7,8]  0b 0x0	0x1 REDA7R8 		[Thunderbolt,GrassKnot,Surf,0x0] ZoningHeal50GroupOneAI
UNIT 0xA0 MagnetonID 0xD0 Level(0, Enemy, 1) [6,10]  0b 0x0	0x1 REDA6R10 	[Thunderbolt,Screech,0x0,0x0] ZoningHeal50GroupOneAI
UNIT 0xA1 ElectrodeID 0xD0 Level(0, Enemy, 1) [8,10]  0b 0x0	0x1 REDA8R10 	[Screech,Explosion,0x0,0x0] ZoningHeal50GroupOneAI
UNIT 0xA2 ElectabuzzID 0xD0 Level(0, Enemy, 1) [4,10]  0b 0x0 0x1 REDA4R10 [ThunderPunch,IcePunch,FirePunch,0x0] ZoningHeal50GroupOneAI
UNIT 0xA3 JolteonID 0xD0 Level(0, Enemy, 1) [10,10]  0b 0x0	0x1 REDA10R10 	[Thunderbolt,SignalBeam, 0x0,0x0] ZoningHeal50GroupOneAI
UNIT 0xA4 MarowakID 0xD0 Level(0, Enemy, 1) [7,12]  0b 0x0	0x1 REDA7R12 	[Bonemerang,RockSlide,0x0,0x0] ZoningHeal50GroupOneAI
UNIT
	

#undef WildLvl
#undef TrainerL
#undef ChapterID