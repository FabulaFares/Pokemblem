
//#define TrainerDefeatedFlagOffset 0xA0  

#include "TrainerDeathFlags.lyn.event"
#include "Quotes.event" 

#define CheckTrainerFlag(UnitID) "SVAL r1 UnitID; ASMC ASMC_CheckTrainerFlag"
#define MarkTrainerAsDefeated(UnitID) "SVAL r1 UnitID; ASMC MarkTrainerAsDefeated_ASMC"
#define UnmarkTrainerAsDefeated(UnitID) "SVAL r1 UnitID; ASMC UnmarkTrainerAsDefeated_ASMC"

ALIGN 4
PostTrainerBattleRamLocatLink:
WORD PostTrainerBattleRamLocat



TrainerSpotsYouEvent:
EVBIT_MODIFY 4
ASMC ModularSummon_UnhideActive 
ASMC ModularSummon_RestoreCameraASMC // Move back to active unit using CenterCameraOntoPosition
CUMO 0x7fff
ASMC 0x80790a5 //MU_EndAll //deselect unit
STAL 20 
CURE
SVAL s5 0 // Pokeball animation as the default 
CHECK_ACTIVE 
SVAL r7 0xEF 
BEQ 20 rC r7 // If unit is 0xEF, always considered cutscene I guess 
SVAL s7 0xEE // Wild Boss pokemon that can only be attacked once its summons are defeated 
BEQ 21 sC s7 
SVAL s7 0xED // Wild Boss pokemon that can only be attacked once its summons are defeated 
BEQ 22 sC s7 
LABEL 0

SVAL 0xB 0x00B0FFFF; SOLOTEXTBOXSTART; TEXTSHOW (-1); TEXTEND //; REMA //BottomWallS2Text 
ASMC CallCheckTrainerFlag 
BEQ 5 rC r0 // If flag is unset, always call modular summon 

EVBIT_MODIFY 4
SVAL r7 1 
EVBIT_F 2 
TEXTSHOW RematchText // Only rematch if player says to 
TEXTEND 
BNE 10 rC r7

LABEL 5 
REMA 
ASMC ModularSummonEffect
GOTO 0x63 

LABEL 10 
SetUnitStatus(0x7FFF, AnimaExp, 50) // So we can talk to them if declined rematch 
REMA 
GOTO 0x63 

LABEL 20 // Playable cutscene eg. rival battle 
ENUT PlayableCutsceneFlag
TEXTSTART; TEXTSHOW (-1); TEXTEND; REMA 
ASMC ModularSummonEffect 
GOTO 0x63 

LABEL 21 // Wild Boss pokemon that can only be attacked once its summons are defeated 

CHECK_EVENTID BossPokemonWarningFlag
BNE 41 sC s0 
TutText(BossPokemonWarningText)
ENUT BossPokemonWarningFlag 
LABEL 41 

ASMC CallCheckTrainerFlag 
BNE 60 rC r0 // If flag is unset, always call modular summon
ENUT PlayableCutsceneFlag
SVAL 0xB 0x00B0FFFF; SOLOTEXTBOXSTART; TEXTSHOW (-1); TEXTEND //; REMA //BottomWallS2Text 
REMA 
SVAL s1 0 //3<<8Attack in range  //0 pursue and attack  
CHAI 0xEE
TutText(AuraFlareText)

SVAL s5 1 // not pokeball 
ASMC ModularSummonEffect
GOTO 0x63 

LABEL 22 // Vesly self-insert
ENUT PlayableCutsceneFlag
STAL 20 
CURE
SVAL 0xB 0x0000FFFF; SOLOTEXTBOXSTART; TEXTSHOW (-1); TEXTEND 
REMA 
ASMC ModularSummonEffect 
GOTO 0x63 

LABEL 60 // if you got here, you probably just forgot to unmark 0xEE boss pkmn flag as defeated in your start event 
// UnmarkTrainerAsDefeated(0xEE)
TutText(ErrorText) // Error 

LABEL 0x63 
EVBIT_MODIFY 0
NoFade 
ENDA 

TrainerDefeatedEvent:
VeslySetDangerRadius
//ASMC BreakPointASMC
//BottomWallText(ViridianForestTrainerE4DefeatText)
EVBIT_MODIFY 4
ASMC PostTrainerBattleActions 
SADD rA rC r0 
ASMC ModularSummon_CenterCameraASMC //SHORT 0x2629 0xFFFE // CAM2 - older EA doesn't have _0x2629 defined & this can freeze the game 
SADD s8 sB s0 
CUMO 0x7ffE
STAL 20 
CURE 
CHECK_EVENTID PlayableCutsceneFlag 
BNE 20 rC r0 

LABEL 10
BottomWallS2Text
BNE 34 rA r0  // award 1/4 gold for rematches 
GOTO 35 
LABEL 34 
SVAL sC 2 
SLSR s3 s3 sC 

LABEL 35 
// just in case... 
SVAL rC 16 
SLSL r3 r3 rC
SLSR r3 r3 rC // so never over 65535  
GIVETOMAIN 0
GOTO 0x63 

LABEL 20 // Cutscene 
SVAL r7 0xEF
BEQ 21 r7 r9 // Only if unit is 0xEF
SVAL s7 0xEE 
BEQ 22 s7 s9 // if unit is wild boss pkmn 
SVAL s7 0xED 
BEQ 23 s7 s9 // if unit is Vesly 
GOTO 10
LABEL 21 
ENUF PlayableCutsceneFlag 
TEXTSTART; TEXTSHOW (-1); TEXTEND; REMA 
FADI 8 
DISA 0xEF 
FADU 10
GOTO 0x63 

LABEL 23 // Vesly unit 0xED before league 
SVAL 0xB 0x0000FFFF; SOLOTEXTBOXSTART; TEXTSHOW (-1); TEXTEND 
FADI 8 
DISA 0xED 
FADU 10
GOTO 0x63 

LABEL 22 
//BottomWallS2Text
TutText(AuraDissipated)
SADD sB s8 s0 
GetUnitStatus(0x7FFe, UnitPointer)
SADD s5 sC s0 
SVAL s6 52*16 //@ reduce by 0x10 unit IDs 
SSUB s5 s5 s6 
SetUnitStatus(0x7FFe, UnitPointer) // 0xEE -> 0xDE unit ID 
//SetUnitStatus(0x7FFF, UnitPointer, 0x8806A48) // 0xEE -> 0xDE unit ID 

// Later - 
// ENUF PlayableCutsceneFlag
// Death quote for the specific pkmn / marowak ? 
GOTO 0x63 



LABEL 0x63 
EVBIT_MODIFY 0
NoFade 
ENDA 

TrainerPostBattleTalkEvent:
ASMC GetTargetAndStoreToRam
ASMC PostTrainerBattleActions 
BottomWallS2Text
NoFade 
ENDA 




ModularSummonWildTrainerEvent: // death quote for unit id 0xDE 
ENUF PlayableCutsceneFlag
ASMC WildAuraMonDefeatQuoteFunc // puts defeated unit's unitID into s6 
ASMC MarkTrainerAsDefeated_ASMC
BEQ 99 s2 s0 // If no quote, skip 
CUMO 0x7ffE
BottomWallS2Text
CURE
SADD s2 s6 s0 // defeated unit ID 

// Special cases 
CHECK_CLASS 0x7ffd 
SVAL s1 MoltresID 
BEQ 3 sC s1 

// Special cases 
CHECK_CLASS 0x7ffd 
SVAL s1 ArticunoID 
BEQ 1 sC s1 

// Special cases 
CHECK_CLASS 0x7ffd 
SVAL s1 ZapdosID 
BEQ 2 sC s1 

// Special cases 
CHECK_CLASS 0x7ffd 
SVAL s1 MewID 
BEQ 0 sC s1 


GOTO 99 

LABEL 0
ENUT MewFledFlag 
GOTO 10 
LABEL 1
ENUT ArticunoFledFlag 
GOTO 10 
LABEL 2
ENUT ZapdosFledFlag 
GOTO 10 
LABEL 3 // Moltres 
ENUT MoltresFledFlag 
GOTO 10 



LABEL 10 // Unit ID to teleport in s2 
WarpOutEvent 
LABEL 99
NoFade 
ENDA 

ModularSummonWildTrainerEvent_LeaderDefeated: // death quote for unit id 0xEE 
CALL ModularSummonWildTrainerEvent // identical for now 
NoFade 
ENDA 


